#Использовать asserts
#Использовать "../src"

&Тест
Процедура ЛямбдаВыполняется() Экспорт

	// Дано

	Параметры = Новый Массив();
	Параметры.Добавить(1);
	Параметры.Добавить(2);

	// Когда

	Обещание = Обещания.ВыполнитьЛямбду(
		"(Первое, Второе) -> Возврат СложитьСЗадержкой(Первое, Второе)",
		ЭтотОбъект,
		Параметры
	);

	// Тогда

	Утверждения.ПроверитьРавенство(Обещание.Получить(), 3);

КонецПроцедуры

&Тест
Процедура ДелегатВыполняется() Экспорт

	// Дано

	Параметры = Новый Массив();
	Параметры.Добавить(1);
	Параметры.Добавить(2);

	// Когда

	Обещание = Обещания.ВыполнитьДелегат(
		Новый Действие(ЭтотОбъект, "СложитьСЗадержкой"),
		Параметры
	);

	// Тогда

	Утверждения.ПроверитьРавенство(Обещание.Получить(), 3);

КонецПроцедуры

&Тест
Процедура ОжидаемВсе() Экспорт

	// Дано

	Параметры = Новый Массив();
	Параметры.Добавить(1);
	Параметры.Добавить(2);

	Ждем = Новый Массив();

	// BSLLS:DuplicatedInsertionIntoCollection-off
	// BSLLS:DuplicateStringLiteral-off
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	// BSLLS:DuplicatedInsertionIntoCollection-on
	// BSLLS:DuplicateStringLiteral-on

	// Когда

	Обещания.ОжидатьВсе(Ждем);

	// Тогда

	Для каждого Обещание Из Ждем Цикл

		Утверждения.ПроверитьРавенство(Обещание.Состояние(), СостояниеФоновогоЗадания.Завершено);
		Утверждения.ПроверитьРавенство(Обещание.Получить(), 3);

	КонецЦикла;

КонецПроцедуры

Процедура НеДождалисьВсех() Экспорт

	// Дано

	Параметры = Новый Массив();
	Параметры.Добавить(1);
	Параметры.Добавить(2);

	Ждем = Новый Массив();

	// BSLLS:DuplicatedInsertionIntoCollection-off
	// BSLLS:DuplicateStringLiteral-off
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	Ждем.Добавить(Обещания.ВыполнитьЛямбду("(П, В) -> Возврат СложитьСЗадержкой(П, В)", ЭтотОбъект, Параметры));
	// BSLLS:DuplicatedInsertionIntoCollection-on
	// BSLLS:DuplicateStringLiteral-on

	// Тогда

	Параметры = Новый Массив;
	Параметры.Добавить(Ждем);
	Параметры.Добавить(10);

	Ожидаем.Что(Обещания)
		.Метод("ОжидатьВсе", Параметры)
		.ВыбрасываетИсключение("Превышено время ожидания получения результата");

КонецПроцедуры

Функция СложитьСЗадержкой(Первое, Второе) Экспорт

	Приостановить(100);

	Возврат Первое + Второе;

КонецФункции
