#Использовать fluent

#Область ОписаниеПеременных

Перем Рефлектор; // Для доступа к приватным полям обещания

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗапуститьВыполнениеОбещания(Обещание) Экспорт

	Параметры = Новый Массив;
	Параметры.Добавить(Обещание);

	Длительное = Рефлектор.ПолучитьСвойство(Обещание, "КонтекстВыполнения").Длительное();

	Задание = ФоновыеЗадания.Выполнить(ЭтотОбъект, "ВыполнитьОбещание", Параметры, Длительное);

	Рефлектор.УстановитьСвойство(Обещание, "Задание", Задание);

КонецПроцедуры

Функция ВыполнитьОбещание(Обещание) Экспорт

	Контекст = Рефлектор.ПолучитьСвойство(
		Обещание,
		"КонтекстВыполнения"
	);

	Если Контекст.Предыдущий() <> Неопределено Тогда

		Если Контекст.ПерехватчикИсключения() Тогда

			Попытка
				Результат = Контекст.Предыдущий()
					.Получить();
			Исключение
				Результат = Контекст.Алгоритм()
					.Выполнить(ИнформацияОбОшибке());
			КонецПопытки;

			Возврат Результат;

		Иначе

			Возврат Контекст.Алгоритм()
				.Выполнить(Контекст.Предыдущий().Получить());

		КонецЕсли;

	Иначе

		Параметры = Контекст.Параметры();

		СтрокаПараметров = Новый Массив();

		Для Счетчик = 0 По Параметры.ВГраница() Цикл
			СтрокаПараметров.Добавить(СтрШаблон("Параметры[%1]", Счетчик));
		КонецЦикла;

		Возврат Рефлектор.ВызватьМетод(Контекст.Алгоритм(), "Выполнить", Параметры);

	КонецЕсли;

КонецФункции

Процедура ОжидатьВсе(Обещания, Таймаут) Экспорт

	Задания = ПроцессорыКоллекций.ИзКоллекции(Обещания)
		.Обработать("Обещание -> Рефлектор.ПолучитьСвойство(Обещание, ""Задание"")", ЭтотОбъект)
		.ВМассив();

	Результат = ФоновыеЗадания.ОжидатьВсе(Задания, Таймаут);

	Если Не Результат Тогда
		ВызватьИсключение "Превышено время ожидания получения результата";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Рефлектор = Новый Рефлектор();
